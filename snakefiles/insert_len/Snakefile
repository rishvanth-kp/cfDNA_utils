rule all:
  input:
    expand('fastqc/{sample}_R1_001_fastqc.html', sample=config['samples']),
    expand('insert_sz/{sample}_insert_sz.pdf', sample=config['samples'])

rule fastqc:
  input:
    r1 = 'reads/{sample}_R1_001.fastq.gz',
    r2 = 'reads/{sample}_R2_001.fastq.gz'
  output:
    o1 = 'fastqc/{sample}_R1_001_fastqc.html',
    o2 = 'fastqc/{sample}_R2_001_fastqc.html'
  params:
    'fastqc'
  shell:
    '{config[fastqc]} {input.r1} {input.r2} -o {params}'

rule mapReads:
  input:
    ref = config['ref'],
    r1 = 'reads/{sample}_R1_001.fastq.gz',
    r2 = 'reads/{sample}_R2_001.fastq.gz'
  output:
    temp('mapped_reads/{sample}.sam')
  threads: 8
  log:
    'logs/{sample}_bwa.log'
  benchmark:
    'logs/{sample}_bwa.benchmark'
  shell:
    '{config[bwa]} mem -t {threads} {input.ref} {input.r1} {input.r2} '
    '1> {output} 2> {log}'

rule sortReads:
  input:
    'mapped_reads/{sample}.sam'
  output:
    'mapped_reads/{sample}_sorted.bam'
  threads: 8
  shell:
    'samtools sort -@ {threads} -o {output} {input}'

rule rmdupReads:
  input:
    'mapped_reads/{sample}_sorted.bam'
  output:
    'mapped_reads/{sample}_rmdup.bam'
  shell:
    'samtools rmdup {input} {output}'

rule rmAmbigReads:
  input:
    'mapped_reads/{sample}_rmdup.bam'
  output:
    'mapped_reads/{sample}_unique.bam'
  threads: 8
  params:
    minMapq = config['minMapq']
  shell:
    'samtools view -@ {threads} -b -q {params.minMapq} -o {output} '
    '{input}'

rule insertSz:
  input:
    'mapped_reads/{sample}_unique.bam'
  output:
    pdf = 'insert_sz/{sample}_insert_sz.pdf',
    txt = 'insert_sz/{sample}_insert_sz.txt'
  log:
    'logs/{sample}_picard.log'
  benchmark:
    'logs/{sample}_picard.benchmark'
  shell:
    'java -jar {config[picard]} CollectInsertSizeMetrics I={input} '
    'O={output.txt} H={output.pdf} 2> {log}'
