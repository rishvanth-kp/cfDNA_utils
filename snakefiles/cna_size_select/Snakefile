# Copyright (C) 2020 Rishvanth Prabakar
#
# Authors: Rish Prabakar
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

from glob import glob
from numpy import unique

reads = glob('{}/*'.format(config['readsDir']))
samples = []
for i in reads:
  sampleName = i.replace('{}/'.format(config['readsDir']), '')
  sampleName = sampleName.replace('{}'.format(config['read1Suffix']), '')
  sampleName = sampleName.replace('{}'.format(config['read2Suffix']), '')
  samples.append(sampleName)

samples = unique(samples)

rule all:
  input:
    expand('fastqc/{sample}_R1_001_fastqc.html', sample=samples),
    expand('insert_sz/{sample}_insert_sz.pdf', sample=samples),
    expand('cna/{sample}.pdf', sample=samples),
    expand('cna_size_selected/{sample}_size_selected.pdf', sample=samples)

rule fastqc:
  input:
    r1 = config['readsDir'] + '/{sample}_R1_001.fastq.gz',
    r2 = config['readsDir'] + '/{sample}_R2_001.fastq.gz'
  output:
    o1 = 'fastqc/{sample}_R1_001_fastqc.html',
    o2 = 'fastqc/{sample}_R2_001_fastqc.html'
  params:
    'fastqc'
  shell:
    '{config[fastqc]} {input.r1} {input.r2} -o {params}'

rule mapReads:
  input:
    ref = config['ref'],
    r1 = config['readsDir'] + '/{sample}_R1_001.fastq.gz',
    r2 = config['readsDir'] + '/{sample}_R2_001.fastq.gz'
  output:
    sam = temp('mapped_reads/{sample}.sam'),
    flagstat = 'flagstat/{sample}_mapped_flagstat.txt'
  threads: config['nThreads']
  log:
    'logs/{sample}_bwa.log'
  benchmark:
    'logs/{sample}_bwa.benchmark'
  shell:
    '{config[bwa]} mem -t {threads} {input.ref} {input.r1} {input.r2} '
    '1> {output.sam} 2> {log}; '
    'samtools flagstat {output.sam} > {output.flagstat}'

rule sortReads:
  input:
    'mapped_reads/{sample}.sam'
  output:
    'mapped_reads/{sample}_sorted.bam'
  threads: config['nThreads']
  shell:
    'samtools sort -@ {threads} -o {output} {input}'

rule removeDuplicates:
  input:
    'mapped_reads/{sample}_sorted.bam'
  output:
    bam = temp('mapped_reads/{sample}_rmdup.bam'),
    flagstat = 'flagstat/{sample}_rmdup_flagstat.txt'
  shell:
    'samtools rmdup {input} {output.bam}; '
    'samtools flagstat {output.bam} > {output.flagstat}'

rule removeAmbig:
  input:
    'mapped_reads/{sample}_rmdup.bam'
  output:
    bam = 'mapped_reads/{sample}_unique.bam',
    flagstat = 'flagstat/{sample}_unique_flagstat.txt'
  params:
    mapq = config['minMapq']
  threads: config['nThreads']
  shell:
    'samtools view -q {params.mapq} -@ {threads} -o {output.bam} {input}; '
    'samtools flagstat {output.bam} > {output.flagstat}'

rule removeMates:
  input:
    'mapped_reads/{sample}_unique.bam'
  output:
    sam = 'mapped_reads/{sample}_fwd.sam',
    flagstat = 'flagstat/{sample}_fwd_flagstat.txt'
  threads: config['nThreads']
  shell:
    'samtools view -f 0x40 -h -@ {threads} -o {output.sam} {input}; '
    'samtools flagstat {output.sam} > {output.flagstat}'

rule insertSz:
  input:
    'mapped_reads/{sample}_unique.bam'
  output:
    pdf = 'insert_sz/{sample}_insert_sz.pdf',
    txt = 'insert_sz/{sample}_insert_sz.txt'
  log:
    'logs/{sample}_picard.log'
  benchmark:
    'logs/{sample}_picard.benchmark'
  shell:
    'java -jar {config[picard]} CollectInsertSizeMetrics I={input} '
    'O={output.txt} H={output.pdf} 2> {log}'

rule cnaAnalysis:
  input:
    sam = 'mapped_reads/{sample}_fwd.sam',
    chromSizes = config['chromSizes'],
    binBounds = config['binBounds'],
    gc = config['gc'],
    badBins = config['badBins']
  output:
    counts = 'cna/{sample}_bincounts.bed',
    stats = 'cna/{sample}_stats.txt',
    cnaPlot = 'cna/{sample}.pdf'
  params:
    sampleName = '{sample}',
    outDir = 'cna'
  shell:
    '{config[binCounts]} -i {input.sam} -c {input.chromSizes} '
    '-b {input.binBounds} -o {output.counts} -s {output.stats}; '
    '{config[cbs]} {output.counts} {params.sampleName} {input.gc} '
    '{input.badBins} {params.outDir}'

rule sizeSelect:
  input:
    'mapped_reads/{sample}_fwd.sam'
  output:
    'mapped_reads/{sample}_size_selected.sam'
  params:
    minInsertLen = config['minInsertLen'],
    maxInsertLen = config['maxInsertLen']
  shell:
    '{config[filterInsertLen]} -s {input} -l {params.minInsertLen} '
    '-u {params.maxInsertLen} -o {output}'

rule sizeSelectedCnaAnalysis:
  input:
    sam = 'mapped_reads/{sample}_size_selected.sam',
    chromSizes = config['chromSizes'],
    binBounds = config['binBounds'],
    gc = config['gc'],
    badBins = config['badBins']
  output:
    counts = 'cna_size_selected/{sample}_size_selected_bincounts.bed',
    stats = 'cna_size_selected/{sample}_size_selected_stats.txt',
    cnaPlot = 'cna_size_selected/{sample}_size_selected.pdf'
  params:
    sampleName = '{sample}_size_selected',
    outDir = 'cna_size_selected'
  shell:
    '{config[binCounts]} -i {input.sam} -c {input.chromSizes} '
    '-b {input.binBounds} -o {output.counts} -s {output.stats}; '
    '{config[cbs]} {output.counts} {params.sampleName} {input.gc} '
    '{input.badBins} {params.outDir}'
